=== Meet the Interpreter

Start up durexForth.
The system will greet you with a blinking yellow cursor, waiting for your input.
This is the interpreter, which allows you to enter Forth code interactively.

Let us try the traditional first program: Type in `.( Hello, world! )` (and press kbd:[Return]).
The system will reply `Hello, world! ok`.
The `ok` means that the system is healthy and ready to accept more input.

Now, let us try some mathematics.
`1 1 +` (followed by kbd:[Return]) will add 1 and 1, leaving 2 on the stack.
This can be verified by entering `.s` to print the stack contents.
Now enter `.` to pop the 2 and print it to screen, followed by another `.s` to verify that the stack is empty.

Let us define a word `bg!` for setting the border color...

----
: bg! $d020 c! ;
----

Now try entering `1 bg!` to change the border color to white.
Then, try changing it back again with `0 bg!`.

=== Introducing the Editor

The v editor is convenient for editing larger pieces of code. With it, you keep an entire source file loaded in RAM, and you can recompile and test it easily.

Start the editor by typing `v`. You will enter the red editor screen. To enter text, first press kbd:[i] to enter insert mode. This mode allows you to insert text into the buffer. You can see that it's active on the `I` that appears in the lower left corner. This is a good start for creating a program!

Now, enter the following lines...

----
: flash begin 1 $d020 +! again ; flash
----

...and then press ‚Üê to leave insert mode.
Press kbd:[F7] to compile and run. If everything is entered right, you will see a beautiful color cycle.

When you finished watching, press kbd:[RESTORE] to quit your program, then enter `v` to reopen the editor.

=== Assembler

If you want to color cycle as fast as possible, it is possible to use the durexForth assembler to generate machine code. `code` and `end-code` define a code word, just like `:` and `;` define Forth words. Within a code word, you can use assembler mnemonics.

----
code flash
here ( push current addr )
$d020 inc,
jmp, ( jump to pushed addr )
end-code
flash
----

It is also possible to use inline assembly within regular Forth words:

----
: flash begin [ $d020 inc, ] again ;
flash
----

IMPORTANT: As the +x+ register contains the parameter stack depth, your assembly code must leave it unchanged.

=== Console I/O Example

This piece of code reads from keyboard and sends back the chars to screen:

----
: foo key emit recurse ;
foo
----

=== Printer Example

This piece of code prints a message to a printer on device #4, and then prints a message to the screen:

----
include io

: print-hello
4 device ( use device 4 )
0 0 47 7 open ioabort ( open address 7 as file 47, abort on failure )
47 chkout ioabort ( redirect output to file 47, abort on failure )
." Hello, printer!" cr
clrchn ( stop input and output redirection )
." Hello, screen!" cr
47 close ( close file 47 ) ;

print-hello
----

The device number and address may differ between printer models. Commodore MPS series printers use address 0 to print in their uppercase/graphics font, and address 7 to print in their lowercase/uppercase font.
